
/*
 * This file is part of Eqela JSH
 * Copyright (c) 2018-2020 Eqela Oy
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation; version 3 of the License.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program. If not, see <https://www.gnu.org/licenses/>.
 */

import jk.fs
import jk.log
import jk.console

class is ConsoleApplicationMain:

// FIXME: This is C# specific implementation only.

depend "nuget:sharpcompress@0.21.1"

var dir as File
var output as File
var truncateDir = false

func initialize override as bool
{
	foreach arg in args {
		if arg.isParameter() {
			assert dir == null:
				Log.error(ctx, "Too many parameters")
			dir = File.forPath(arg.parameter)
		}
		else if arg.isFlag("truncateDir") || arg.isFlag("truncatedir") || arg.isFlag("flat") {
			truncateDir = true
		}
		else if arg.isOption("output") {
			output = File.forPath(arg.value)
		}
		else {
			arg.reportAsUnsupported(ctx)
			return false
		}
	}
	return true
}

func execute override as bool
{
	assert dir:
		Log.error(ctx, "No directory provided.")
	assert dir.isDirectory():
		Log.error(ctx, "Not a directory: `" .. dir.getPath() .. "'")
	var bn = dir.baseName()
	assert String.isEmpty(bn) == false:
		Log.error(ctx, "Empty basename: `" .. dir.getPath() .. "'")
	var pd = assert dir.getParent():
		Log.error(ctx, "Directory has no parent: `" .. dir.getPath() .. "'")
	var parentPath = pd.getPath()
	assert String.isNotEmpty(parentPath):
		Log.error(ctx, "Parent path is empty: `" .. dir.getPath() .. "'")
	var parentPathLength = String.getLength(parentPath)
	if truncateDir:
		parentPathLength = String.getLength(dir.getPath())
	var of = output
	if of == null:
		of = pd.entry(bn .. ".tar.gz")
	lang "cs" {{{
		using(System.IO.Stream stream = System.IO.File.OpenWrite(of.getPath()))
		using(var writer = SharpCompress.Writers.WriterFactory.Open(stream, SharpCompress.Common.ArchiveType.Tar, new SharpCompress.Writers.WriterOptions(SharpCompress.Common.CompressionType.GZip) {
			LeaveStreamOpen = true
		}))
		{
	}}}
	Log.status(ctx, "[tgz] " .. of.getPath() .. " ..")
	var ff = FileFinder.forRoot(dir)
	loop {
		var file = ff.next()
		if not file:
			break
		if file.isFile() == false:
			continue
		var fpath = file.getPath()
		if String.isEmpty(fpath):
			continue
		var partialPath = String.getEndOfString(fpath, parentPathLength + 1)
		if String.isEmpty(partialPath):
			continue
		partialPath = String.replaceCharacter(partialPath, '\\', '/')
		Log.status(ctx, "[tgz] " .. file.getPath() .. " ..")
		lang "cs" {{{
			using(System.IO.Stream fstream = System.IO.File.OpenRead(file.getPath())) {
				writer.Write(partialPath, fstream, new System.IO.FileInfo(file.getPath()).LastWriteTime);
			}
		}}}
	}
	lang "cs" {{{
		}
	}}}
	return true
}
